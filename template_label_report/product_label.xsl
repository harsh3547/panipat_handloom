<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:fo="http://www.w3.org/1999/XSL/Format">
	<xsl:variable name="initial_bottom_pos">26.5</xsl:variable>
	<xsl:variable name="initial_left_pos">0.7</xsl:variable>
	<xsl:variable name="height_increment">2.0</xsl:variable>
	<xsl:variable name="width_increment">6.6</xsl:variable>
	<xsl:variable name="frame_height">2.2cm</xsl:variable>
	<xsl:variable name="frame_width">6.4cm</xsl:variable>
	<xsl:variable name="number_columns">3</xsl:variable>
	<xsl:variable name="max_frames">42</xsl:variable>
	<!-- 
	<xsl:variable name="number_columns">1</xsl:variable>
	<xsl:variable name="max_frames">10</xsl:variable>
	 -->
	<xsl:template match="/">
		<xsl:apply-templates select="lots"/>
	</xsl:template>
	<xsl:template match="lots">
		<document>
		<!-- <template pageSize="(10.9cm,29.7cm)" leftMargin="2.0cm" rightMargin="2.0cm" topMargin="2.0cm" bottomMargin="2.0cm" title="Address list" author="Generated by Odoo">
			 -->
			<template showBoundary="0" leftMargin="1.0cm" rightMargin="1.0cm" topMargin="1.0cm" bottomMargin="1.0cm" title="Address list" author="Generated by Odoo">
				<pageTemplate id="all">
					<pageGraphics/>
					<xsl:apply-templates select="lot-line" mode="frames"/>
				</pageTemplate>
			</template>
			<stylesheet>
				<paraStyle name="nospace" fontName="Courier-Bold" fontSize="12" spaceBefore="0" spaceAfter="0"/>
				<paraStyle name="nospace_1" fontName="Courier" fontSize="10" spaceBefore="0" spaceAfter="0"/>
				
				<blockTableStyle id="mytable">
					<blockAlignment value="CENTER"/>
					<blockValign value="MIDDLE"/>
					<!-- <blockTopPadding length="0cm" /> -->
					<!-- <blockBottomPadding length="0cm" />  -->
					<blockRightPadding length="0cm" />
					<lineStyle kind="GRID" colorName="black" tickness="1"/>
				</blockTableStyle>
			</stylesheet>
			<story>
				<xsl:apply-templates select="lot-line" mode="story"/>
			</story>
		</document>
	</xsl:template>

	<xsl:template match="lot-line" mode="frames">
		<xsl:if test="position() &lt; $max_frames + 1">
			<frame>
				<xsl:attribute name="width">
					<xsl:value-of select="$frame_width"/>
				</xsl:attribute>
				<xsl:attribute name="height">
					<xsl:value-of select="$frame_height"/>
				</xsl:attribute>
				<xsl:attribute name="x1">
					<xsl:value-of select="$initial_left_pos + ((position()-1) mod $number_columns) * $width_increment"/>
					<xsl:text>cm</xsl:text>
				</xsl:attribute>
				<xsl:attribute name="y1">
					<xsl:value-of select="$initial_bottom_pos - floor((position()-1) div $number_columns) * $height_increment"/>
					<xsl:text>cm</xsl:text>
				</xsl:attribute>
			</frame>
		</xsl:if>
	</xsl:template>

	<xsl:template match="lot-line" mode="story">
		<blockTable style="mytable" colWidths="2.5cm,3.9cm">
			<tr>
				<td>
		           <para style="nospace"><xsl:value-of select="code"/></para>
            	</td>
            	<td>
            		<xsl:if test="serial!=''">
            			<para style="nospace_1">S/N:<xsl:value-of select="substring(serial, 1, 13)"/></para>
            		</xsl:if>
            		<xsl:if test="serial='' and shade!=''">
            			<para style="nospace_1">Sh/N:<xsl:value-of select="substring(shade, 1, 12)"/></para>
            		</xsl:if>
            		<xsl:if test="serial='' and shade='' and colorc!=''">
            			<para style="nospace_1">C/C:<xsl:value-of select="substring(colorc, 1, 13)"/></para>
            		</xsl:if>
            		<xsl:if test="serial='' and shade='' and colorc='' and design!=''">
            			<para style="nospace_1">D/N:<xsl:value-of select="substring(design, 1, 13)"/></para>
            		</xsl:if>
            	</td>
            </tr>
         </blockTable>
        <blockTable style="mytable" colWidths="6.4cm">
            <tr>
            	<td>
            		<xsl:if test="string-length(normalize-space(brand)) &lt; 13 and string-length(normalize-space(file)) &lt; 16">	
            			<para style="nospace_1"><xsl:value-of select="brand"/>/<xsl:value-of select="file"/></para>
            		</xsl:if>
            		
            		<xsl:if test="string-length(normalize-space(brand)) &gt; 12 and string-length(normalize-space(file)) &gt; 15">	
            			<para style="nospace_1"><xsl:value-of select="substring(brand, 1, 12)"/>/<xsl:value-of select="substring(file, 1, 15)"/></para>
            		</xsl:if>
            		
            		<xsl:if test="string-length(normalize-space(brand)) &gt; 12 and string-length(normalize-space(file)) &lt; 16">	
            			<para style="nospace_1"><xsl:value-of select="substring(brand, 1, 27 - string-length(normalize-space(file)))"/>/<xsl:value-of select="substring(file, 1, 15)"/></para>
            		</xsl:if>
            		
            		<xsl:if test="string-length(normalize-space(brand)) &lt; 13 and string-length(normalize-space(file)) &gt; 15">	
            			<para style="nospace_1"><xsl:value-of select="substring(brand, 1, 12)"/>/<xsl:value-of select="substring(file, 1, 27 - string-length(normalize-space(brand)))"/></para>
            		</xsl:if>
            		
            		
		            <para style="nospace_1"><xsl:value-of select="substring(product, 1, 28)"/></para>
            	</td>
            </tr>
		</blockTable>
		<nextFrame/>
	</xsl:template>
</xsl:stylesheet>
